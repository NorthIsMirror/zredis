# Tests for the zdharma/redis module

%prep

 module_path=( `pwd`/Modules )
 modname="zdharma/zredis"
 db1="127.0.0.1:6379/10"
 db2="127.0.0.1/11"
 if ! zmodload $modname ; then
   ZTST_unimplemented="can't load $modname module for testing"
 fi
 redis-cli -n 10 flushdb
 redis-cli -n 11 flushdb

%test

 (zmodload -u $modname && zmodload $modname)
0:unload and reload the module without crashing

 zrtie -d db/redis -f $db1 dbase
 dbase[testkey]=testdata
 zruntie dbase
 unset dbase
 zrtie -d db/redis -f $db1 dbase
 echo $dbase[testkey]
 zruntie dbase
 echo $zredis_tied ${#zredis_tied}
0:store key in database 1
>testdata
>0

 zrtie -d db/redis -f $db2 dbase
 dbase[testkey]=testdata
 zruntie dbase
 unset dbase
 zrtie -d db/redis -f $db2 dbase
 echo $dbase[testkey]
 zruntie dbase
 echo $zredis_tied ${#zredis_tied}
0:store key in database 2
>testdata
>0

 zrtie -d db/redis -f $db1 dbase2
 unset 'dbase2[testkey]'
 zruntie dbase2
 zrtie -d db/redis -f $db1 dbase
 echo $dbase[testkey]
 zruntie dbase
 echo $zredis_tied ${#zredis_tied}
0:remove key from database 1 (different variables)
>
>0

 zrtie -d db/redis -f $db1 dbase
 dbase[testkey]=testdata
 zruntie dbase
 zrtie -d db/redis -f $db1 dbase
 echo $dbase[testkey]
 zruntie dbase
 zrtie -d db/redis -f $db1 dbase
 unset 'dbase[testkey]'
 zruntie dbase
 zrtie -d db/redis -f $db1 dbase
 echo $dbase[testkey]
 zruntie dbase
 echo $zredis_tied ${#zredis_tied}
0:store & remove key from database (the same variables)
>testdata
>
>0

 zrtie -d db/redis -f $db1 dbase
 dbase[testkey]=testdata
 dbase[testkey2]=$dbase[testkey]
 dbase[testkey3]=$dbase[testkey]x$dbase[testkey2]
 zruntie dbase
 zrtie -d db/redis -f $db1 dbase
 echo $dbase[testkey]
 echo $dbase[testkey2]
 echo $dbase[testkey3]
 zruntie dbase
 echo $zredis_tied ${#zredis_tied}
0:store 2 keys fetching 1st
>testdata
>testdata
>testdataxtestdata
>0

 zrtie -d db/redis -f $db1 dbase
 val=$dbase[testkey2]
 unset 'dbase[testkey2]'
 echo $val
 zruntie dbase
 echo $zredis_tied ${#zredis_tied}
0:unset key that was fetched
>testdata
>0

 zrtie -d db/redis -f $db1 dbase
 local -a result
 result=( "${(kv)dbase[@]}" )
 print -rl -- "${(o)result[@]}"
 zruntie dbase
 echo $zredis_tied ${#zredis_tied}
0:scan read-only tied hash, directly assign local -a
>testdata
>testdataxtestdata
>testkey
>testkey3
>0

 zrtie -d db/redis -f $db1 dbase
 dbase=( a a )
 print -rl -- "${(kv)dbase[@]}"
 zruntie dbase
 echo $zredis_tied ${#zredis_tied}
0:Use scan directly after overwrite of database
>a
>a
>0

 zrtie -d db/redis -f $db1 dbase
 dbase=( a b c d )
 zruntie dbase
 unset dbase
 zrtie -d db/redis -f $db1 dbase
 result=( "${(kv)dbase[@]}" )
 print -rl -- "${(o)result[@]}"
 zruntie dbase
 echo $zredis_tied ${#zredis_tied}
0:replace hash / database with untie, scan
>a
>b
>c
>d
>0

 zrtie -d db/redis -f $db1 dbase
 local -a arr
 arr=( "${dbase[@]}" )
 print -rl -- "${(o)arr[@]}"
 zruntie dbase
 echo $zredis_tied ${#zredis_tied}
0:scan with no (kv)
>b
>d
>0

 zrtie -d db/redis -f $db1 dbase
 result=( "${(k)dbase[@]}" )
 print -rl -- "${(o)result[@]}"
 zruntie dbase
 echo $zredis_tied ${#zredis_tied}
0:scan with keys only - (k)
>a
>c
>0

 zrtie -d db/redis -f $db1 dbase
 result=( "${(v)dbase[@]}" )
 print -rl -- "${(o)result[@]}"
 zruntie dbase
 echo $zredis_tied ${#zredis_tied}
0:scan with values only, explicit - (v)
>b
>d
>0

 zrtie -d db/redis -f ${db1%/*}/1000 dbase 2>/dev/null; ret=$?
 echo $zredis_tied ${#zredis_tied}
 return $ret
1:read-only open non-existent database
>0

 zrtie -d db/redis -f $db1 dbase
 dbase=( )
 dbase+=( a b )
 echo $dbase[a]
 zruntie dbase
 zrtie -d db/redis -f $db1 dbase
 echo $dbase[a]
 result=( "${(kv)dbase[@]}" )
 print -rl -- "${(o)result[@]}"
 zruntie dbase
 zrtie -d db/redis -f $db1 dbase
 dbase+=( c d )
 echo $dbase[a]
 echo $dbase[c]
 result=( "${(kv)dbase[@]}" )
 print -rl -- "${(o)result[@]}"
 zruntie dbase
 zrtie -d db/redis -f $db1 dbase
 echo $dbase[a]
 echo $dbase[c]
 result=( "${(kv)dbase[@]}" )
 print -rl -- "${(o)result[@]}"
 zruntie dbase
0:Append with +=( ), also with existing data, also (kv) scan
>b
>b
>a
>b
>b
>d
>a
>b
>c
>d
>b
>d
>a
>b
>c
>d

 zrtie -d db/redis -f $db1 dbase
 echo ${(t)dbase}
 zruntie dbase
0:Type of tied parameter
>association-special

 typeset -ga dbase
 zrtie -d db/redis -f $db1 dbase
 echo ${(t)dbase}
 zruntie dbase
0:Type of tied parameter, with preceding unset
>association-special

 local -a dbase
 zrtie -d db/redis -f $db1 dbase
 echo ${(t)dbase}
 zruntie dbase
0:Type of tied parameter, with local parameter already existing
>association-local-special

 local -a dbase
 dbase=( fromarray )
 () {
     local -a dbase
     zrtie -d db/redis -f $db1 dbase
     echo ${(t)dbase}
     zruntie dbase
 }
 echo $dbase[1]
 zrtie -d db/redis -f $db1 dbase
 echo "Can connect:" $dbase[a]
 zruntie dbase
0:Test of automatic untie (use of local scope) and of scoping
>association-local-special
>fromarray
>Can connect: b

 echo First $zredis_tied ${#zredis_tied}
 zrtie -d db/redis -f $db1 dbase
 echo $zredis_tied ${#zredis_tied}
 zrtie -d db/redis -f $db2 dbase2
 echo $zredis_tied ${#zredis_tied}
 zruntie dbase
 echo $zredis_tied ${#zredis_tied}
 zruntie dbase2
 echo $zredis_tied ${#zredis_tied}
0:zredis_tied parameter
>First 0
>dbase 1
>dbase dbase2 2
>dbase2 1
>0

 unset zredis_tied 2>/dev/null
1:unset of read-only zgdbm_tied parameter

 zrtie -d db/redis -f $db1 dbase
 dbase[漢字]=漢字
 echo $dbase[漢字]
 zruntie dbase
 zrtie -d db/redis -f $db1 dbase
 echo $dbase[漢字]
 zruntie -u dbase
0:Unicode test
>漢字
>漢字

 key="ab"$'\0'"ef"
 zrtie -d db/redis -f $db1 dbase
 dbase[$key]=value
 echo $dbase[$key]
 zruntie dbase
 zrtie -d db/redis -f $db1 dbase
 echo $dbase[$key]
 zruntie dbase
 zrtie -d db/redis -f $db1 dbase
 dbase[$key]=$key
 zruntie dbase
 zrtie -d db/redis -f $db1 dbase
 [[ "$dbase[$key]" = "$key" ]] && echo correct
 zruntie dbase
0:Metafication of $'\0'
>value
>value
>correct

 zrtie -d db/redis -f $db1 dbase
 dbase=( 漢字 漢字 )
 echo $dbase[漢字]
 zruntie dbase
 zrtie -d db/redis -f $db1 dbase
 echo $dbase[漢字]
 zruntie dbase
 key="ab"$'\0'"ef"
 zrtie -d db/redis -f $db1 dbase
 dbase+=( $key $key )
 zruntie dbase
 zrtie -d db/redis -f $db1 dbase
 [[ "$dbase[$key]" = "$key" ]] && echo correct
 zruntie dbase
0:Unicode & metafication test, different hash access
>漢字
>漢字
>correct

 zrtie -d db/redis -f $db1 dbase
 dbase=( 漢字 漢字 )
 zruntie dbase
 zrtie -d db/redis -f $db1 dbase
 noglob print -rl ${(kv)dbase[@]}
 zruntie dbase
0:Hash scanning and metafication
>漢字
>漢字

 zrtie -d db/redis -f $db1 dbase
 noglob print -rl ${(okv)dbase[@]}
 zruntie dbase
0:Sorted hash scanning and metafication
>漢字
>漢字

 zrtie -d db/redis -f $db1 dbase
 zredishost dbase
 [[ $REPLY = "127.0.0.1:6379/10" ]] && echo correct
 zruntie dbase
 zrtie -d db/redis -f $db2 dbase
 zredishost dbase
 [[ $REPLY = "127.0.0.1/11" ]] && echo correct
 zruntie dbase
0:zredishost builtin
>correct
>correct

 zrtie -d db/redis -f $db1 dbase
 dbase[testkey]=value1
 fun() { while read line; do echo $line; done }
 eval "dbase[testkey]=value2" | fun
 echo $dbase[testkey]
 zredisclear dbase testkey
 echo $dbase[testkey]
0:Test store in forked Zsh
>value1
>value2

 zrtie -p -d db/redis -f $db1 dbase
 dbase[testkey]=value1
 echo $dbase[testkey]
 fun() { while read line; do echo $line; done }
 eval "dbase[testkey]=value2" | fun
 echo $dbase[testkey]
0:Test store in forked Zsh, with -p passthrough
>value1
>value2

%clean

 redis-cli -n 10 flushdb
 redis-cli -n 11 flushdb

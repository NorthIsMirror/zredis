# Tests for the zdharma/redis module

%prep

 module_path=( `pwd`/Modules )
 modname="zdharma/zredis"
 db1="127.0.0.1:6379/10/myzset"
 db2="127.0.0.1/11/myzset"
 if ! zmodload $modname ; then
   ZTST_unimplemented="can't load $modname module for testing"
 fi
 redis-cli -n 10 flushdb
 redis-cli -n 10 zadd myzset 0.0 testkey
 redis-cli -n 11 flushdb
 redis-cli -n 11 zadd myzset 0.0 testkey

%test

 (zmodload -u $modname && zmodload $modname)
0:unload and reload the module without crashing

 zrtie -d db/redis -f $db1 dbase
 dbase[testkey]=1.0
 zruntie dbase
 unset dbase
 zrtie -r -d db/redis -f $db1 dbase
 echo $dbase[testkey]
 zruntie -u dbase
 echo $zredis_tied ${#zredis_tied}
0:store key in database 1
>1
>0

 zrtie -d db/redis -f $db2 dbase
 dbase[testkey]=1.0
 zruntie dbase
 unset dbase
 zrtie -d db/redis -f $db2 dbase
 echo $dbase[testkey]
 zruntie dbase
 echo $zredis_tied ${#zredis_tied}
0:store key in database 2
>1
>0

 zrtie -d db/redis -f $db1 dbase2
 dbase2[testkey2]=2.0
 unset 'dbase2[testkey]'
 zruntie dbase2
 zrtie -d db/redis -f $db1 dbase
 echo $dbase[testkey]
 zruntie dbase
 echo $zredis_tied ${#zredis_tied}
0:remove key from database 1 (different variables)
>
>0

 zrtie -d db/redis -f $db1 dbase
 dbase[testkey]=1.0
 zruntie dbase
 zrtie -d db/redis -f $db1 dbase
 echo $dbase[testkey]
 zruntie dbase
 zrtie -d db/redis -f $db1 dbase
 unset 'dbase[testkey]'
 zruntie dbase
 zrtie -r -d db/redis -f $db1 dbase
 echo $dbase[testkey]
 zruntie -u dbase
 echo $zredis_tied ${#zredis_tied}
0:store & remove key from database (the same variables)
>1
>
>0

 zrtie -d db/redis -f $db1 dbase
 dbase[testkey]=1.0
 dbase[testkey2]=$dbase[testkey]
 dbase[testkey3]=$(( dbase[testkey] + $dbase[testkey2] ))
 zruntie dbase
 zrtie -d db/redis -f $db1 dbase
 echo $dbase[testkey]
 echo $dbase[testkey2]
 echo $dbase[testkey3]
 zruntie dbase
 echo $zredis_tied ${#zredis_tied}
0:store 2 keys fetching 1st
>1
>1
>2
>0

 zrtie -d db/redis -f $db1 dbase
 val=$dbase[testkey2]
 unset 'dbase[testkey2]'
 echo $val
 echo $dbase[testkey2]
 zruntie dbase
 echo $zredis_tied ${#zredis_tied}
0:unset key that was fetched
>1
>
>0

 zrtie -d db/redis -f $db1 dbase
 local -a result
 result=( "${(kv)dbase[@]}" )
 print -rl -- "${(o)result[@]}"
 zruntie dbase
 echo $zredis_tied ${#zredis_tied}
0:scan read-only tied hash, directly assign local -a
>1
>2
>testkey
>testkey3
>0

 zrtie -d db/redis -f $db1 dbase
 echo $dbase[testkey]
 dbase=( a 1.0 )
 print -rl -- "${(kv)dbase[@]}"
 zruntie dbase
 echo $zredis_tied ${#zredis_tied}
0:Use scan directly after overwrite of database
>1
>a
>1
>0

 zrtie -d db/redis -f $db1 dbase
 dbase=( a 1.0 c 2.0 )
 zruntie dbase
 unset dbase
 zrtie -r -d db/redis -f $db1 dbase
 result=( "${(kv)dbase[@]}" )
 print -rl -- "${(o)result[@]}"
 zruntie -u dbase
 echo $zredis_tied ${#zredis_tied}
0:replace hash / database with untie, scan
>1
>2
>a
>c
>0

 zrtie -d db/redis -f $db1 dbase
 local -a arr
 arr=( "${dbase[@]}" )
 print -rl -- "${(o)arr[@]}"
 zruntie dbase
 echo $zredis_tied ${#zredis_tied}
0:scan with no (kv)
>1
>2
>0

 zrtie -d db/redis -f $db1 dbase
 result=( "${(k)dbase[@]}" )
 print -rl -- "${(o)result[@]}"
 zruntie dbase
 echo $zredis_tied ${#zredis_tied}
0:scan with keys only - (k)
>a
>c
>0

 zrtie -d db/redis -f $db1 dbase
 result=( "${(v)dbase[@]}" )
 print -rl -- "${(o)result[@]}"
 zruntie dbase
 echo $zredis_tied ${#zredis_tied}
0:scan with values only, explicit - (v)
>1
>2
>0

 zrtie -d db/redis -f ${db1%/*}/1000 dbase 2>/dev/null; ret=$?
 echo $zredis_tied ${#zredis_tied}
 return $ret
1:read-only open non-existent database
>0

 zrtie -d db/redis -f $db1 dbase
 dbase=( )
 dbase+=( a 1 )
 echo $dbase[a]
 zruntie dbase
 zrtie -d db/redis -f $db1 dbase
 echo $dbase[a]
 result=( "${(kv)dbase[@]}" )
 print -rl -- "${(o)result[@]}"
 zruntie dbase
 zrtie -d db/redis -f $db1 dbase
 dbase+=( c 2 )
 echo $dbase[a]
 echo $dbase[c]
 result=( "${(kv)dbase[@]}" )
 print -rl -- "${(o)result[@]}"
 zruntie dbase
 zrtie -d db/redis -f $db1 dbase
 echo $dbase[a]
 echo $dbase[c]
 result=( "${(kv)dbase[@]}" )
 print -rl -- "${(o)result[@]}"
 zruntie dbase
0:Append with +=( ), also with existing data, also (kv) scan
>1
>1
>1
>a
>1
>2
>1
>2
>a
>c
>1
>2
>1
>2
>a
>c

 zrtie -d db/redis -f $db1 dbase
 echo ${(t)dbase}
 zruntie dbase
0:Type of tied parameter
>association-special

 typeset -ga dbase
 zrtie -d db/redis -f $db1 dbase
 echo ${(t)dbase}
 zruntie dbase
0:Type of tied parameter, with preceding unset
>association-special

 local -a dbase
 zrtie -d db/redis -f $db1 dbase
 echo ${(t)dbase}
 zruntie dbase
0:Type of tied parameter, with local parameter already existing
>association-local-special

 local -a dbase
 dbase=( fromarray )
 () {
     local -a dbase
     zrtie -d db/redis -f $db1 dbase
     echo ${(t)dbase}
     zruntie dbase
 }
 echo $dbase[1]
 zrtie -d db/redis -f $db1 dbase
 echo "Can connect:" $dbase[a]
 zruntie dbase
0:Test of automatic untie (use of local scope) and of scoping
>association-local-special
>fromarray
>Can connect: 1

 echo First $zredis_tied ${#zredis_tied}
 zrtie -d db/redis -f $db1 dbase
 echo $zredis_tied ${#zredis_tied}
 zrtie -d db/redis -f $db2 dbase2
 echo $zredis_tied ${#zredis_tied}
 zruntie dbase
 echo $zredis_tied ${#zredis_tied}
 zruntie dbase2
 echo $zredis_tied ${#zredis_tied}
0:zredis_tied parameter
>First 0
>dbase 1
>dbase dbase2 2
>dbase2 1
>0

 unset zredis_tied 2>/dev/null
1:unset of read-only zgdbm_tied parameter

 zrtie -d db/redis -f $db1 dbase
 dbase[漢字]=1
 echo $dbase[漢字]
 zruntie dbase
 zrtie -d db/redis -f $db1 dbase
 echo $dbase[漢字]
 zruntie -u dbase
0:Unicode test
>1
>1

 key="ab"$'\0'"ef"
 zrtie -d db/redis -f $db1 dbase
 dbase[$key]=1
 echo $dbase[$key]
 zruntie dbase
 zrtie -d db/redis -f $db1 dbase
 echo $dbase[$key]
 zruntie dbase
0:Metafication of $'\0'
>1
>1

 zrtie -d db/redis -f $db1 dbase
 dbase=( 漢字 1 )
 echo $dbase[漢字]
 zruntie dbase
 zrtie -d db/redis -f $db1 dbase
 echo $dbase[漢字]
 zruntie dbase
 key="ab"$'\0'"ef"
 zrtie -d db/redis -f $db1 dbase
 dbase+=( $key 1 )
 zruntie dbase
 zrtie -d db/redis -f $db1 dbase
 [[ "$dbase[$key]" = "1" ]] && echo correct
 zruntie dbase
0:Unicode & metafication test, different hash access
>1
>1
>correct

 zrtie -d db/redis -f $db1 dbase
 dbase=( 漢字 1 )
 zruntie dbase
 zrtie -d db/redis -f $db1 dbase
 noglob print -rl ${(kv)dbase[@]}
 zruntie dbase
0:Hash scanning and metafication
>漢字
>1

 zrtie -d db/redis -f $db1 dbase
 noglob print -rl ${(okv)dbase[@]}
 zruntie dbase
0:Sorted hash scanning and metafication
>1
>漢字

 zrtie -d db/redis -f $db1 dbase
 zredishost dbase
 [[ $REPLY = "127.0.0.1:6379/10/myzset" ]] && echo correct
 zruntie dbase
 zrtie -d db/redis -f $db2 dbase
 zredishost dbase
 [[ $REPLY = "127.0.0.1/11/myzset" ]] && echo correct
 zruntie dbase
0:zredishost builtin
>correct
>correct

 zrtie -d db/redis -f $db1 dbase
 dbase[testkey]=1
 fun() { while read line; do echo $line; done }
 eval "dbase[testkey]=2" | fun
 echo $dbase[testkey]
 zredisclear dbase testkey
 echo $dbase[testkey]
 zruntie dbase
0:Test store in forked Zsh
>1
>2

 zrtie -p -d db/redis -f $db1 dbase
 dbase[testkey]=1
 echo $dbase[testkey]
 fun() { while read line; do echo $line; done }
 eval "dbase[testkey]=2" | fun
 echo $dbase[testkey]
 zruntie dbase
0:Test store in forked Zsh, with -p passthrough
>1
>2

 zrtie -d db/redis -f $db1 dbase
 echo $dbase[a] $dbase[b] $dbase[c]
 unset dbase
 zrtie -r -d db/redis -f $db1 dbase
 echo $dbase[a] $dbase[b] $dbase[c]
 zruntie -u dbase
0:Test references to not-existing elements
>
>

%clean

 redis-cli -n 10 flushdb
 redis-cli -n 11 flushdb
